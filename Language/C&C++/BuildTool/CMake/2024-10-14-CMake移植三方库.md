---
title: "C/C++-构建-CMake-移植三方库"
date: 2024-10-14
categories: [C&C++]
tags: [C&C++]
excerpt: "移植三方库"
---

> [CMake学习笔记05--ExternalProject_Add模块](https://blog.csdn.net/zym326975/article/details/106118063)

# CMake移植三方库

## find_package

若第三方库支持CMake的配置文件, 可以直接通过find_package找到和使用它们

```cmake
# 查找第三方库SomeLibrary
find_package(SomeLibrary REQUIRED)

# 链接库
target_link_libraries(MyExecutable PRIVATE SomeLibrary::SomeLibrary) 
```

这种方式通常需要第三方库在系统上已安装, 或者设置CMAKE_PREFIX_PATH来指定库的安装路径

## 添加库源代码到项目中

若第三方库没有CMake配置文件, 且可以访问其源代码, 可以将库的源代码直接添加到项目中, 并使用add_subdirectory编译:

```cmake
add_subdirectory(path/to/SomeLibrary)

target_link_libraries(MyExecutable PRIVATE SomeLibrary)
```

## 使用FetchContent模块

CMake 3.11及以上版本引入了FetchContent模块, 便于直接从网上下载第三方库的源码并编译

```cmake
include(FetchContent)

FetchContent_Declare(
  googletest
  URL https://github.com/google/googletest/archive/release-1.10.0.tar.gz
)

FetchContent_MakeAvailable(googletest)

target_link_libraries(MyExecutable PRIVATE gtest gtest_main)
```

## ExternalProject

ExternalProject是CMake提供的模块, 适合下载、配置、构建和安装外部项目

它提供了更灵活的管理第三方库的方法, 例如可以在不同的编译阶段完成下载和构建, 并支持多种构建系统(例如autotools、CMake等)

### 用法

```cmake
include(ExternalProject)
ExternalProject_Add(
    MyLibrary
    PREFIX ${CMAKE_BINARY_DIR}/my_library
    GIT_REPOSITORY https://github.com/user/my_library.git
    GIT_TAG master
    CMAKE_ARGS -DCMAKE_INSTALL_PREFIX=<INSTALL_DIR> -DCMAKE_BUILD_TYPE=Release
)
```

```mermaid
graph LR;
    A(参数)
    A --> B(PREFIX) --> B1(设置第三方库的下载和构建目录)
    A --> C(GIT_REPOSITORY) --> C1(第三方库的Git仓库地址)
    A --> D(GIT_TAG) --> D1(Git标签或分支)
    A --> E(CMAKE_ARGS) --> E1(传递给第三方库CMake构建系统的参数)
```

### 链接生成库

ExternalProject通常会将外部项目安装到一个指定的目录。在主项目中使用生成的库时，需要手动添加安装路径

```cmake
ExternalProject_Add(
    MyLibrary
    PREFIX ${CMAKE_BINARY_DIR}/my_library
    GIT_REPOSITORY https://github.com/user/my_library.git
    GIT_TAG master
    CMAKE_ARGS -DCMAKE_INSTALL_PREFIX=${CMAKE_BINARY_DIR}/my_library/install
)

# 添加生成的库
add_library(MyLibrary STATIC IMPORTED)
set_target_properties(MyLibrary PROPERTIES
    IMPORTED_LOCATION ${CMAKE_BINARY_DIR}/my_library/install/lib/libMyLibrary.a
    INTERFACE_INCLUDE_DIRECTORIES ${CMAKE_BINARY_DIR}/my_library/install/include
)

target_link_libraries(MyExecutable PRIVATE MyLibrary)
```


### 示例

- 创建名为gflag-2.2.2.cmake的文件

在项目中创建名为Cmake的文件夹下创建名为gflag-2.2.2.cmake的文件

```cmake
include(ExternalProject)

set(GFLAG_ROOT          ${CMAKE_BINARY_DIR}/thirdparty/gflag-2.2.2)
set(GFLAG_LIB_DIR       ${GFLAG_ROOT}/lib)
set(GFLAG_INCLUDE_DIR   ${GFLAG_ROOT}/include)
 
set(GFLAG_URL           https://github.com/gflags/gflags/archive/v2.2.2.zip)
set(GFLAG_CONFIGURE     cd ${GFLAG_ROOT}/src/gflag-2.2.2 && cmake -D CMAKE_INSTALL_PREFIX=${GFLAG_ROOT} .)
set(GFLAG_MAKE          cd ${GFLAG_ROOT}/src/gflag-2.2.2 && make)
set(GFLAG_INSTALL       cd ${GFLAG_ROOT}/src/gflag-2.2.2 && make install)

ExternalProject_Add(gflag-2.2.2
    URL                   ${GFLAG_URL}
    DOWNLOAD_NAME         gflag-2.2.2.zip
    PREFIX                ${GFLAG_ROOT}
    CONFIGURE_COMMAND     ${GFLAG_CONFIGURE}
    BUILD_COMMAND         ${GFLAG_MAKE}
    INSTALL_COMMAND       ${GFLAG_INSTALL}
)
```

- 在项目根目录下的CMakeLists.txt中使用include调用这个cmake

```cmake
include(cmake/gflag-2.2.2.cmake)
include(cmake/hwloc-1.11.cmake)
include(cmake/protobuf-2.6.1.cmake)
include(cmake/protobuf-2.6.1-compile.cmake)
```